USE [ShopDB_TEST]
GO

/****** Object:  StoredProcedure [sales].[SP_문제2-4_재고수불]    Script Date: 2022-04-18 오후 2:56:00 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO



/*--------------------------------------------------------
EXEC [sales].[SP_문제2-4_재고수불] @IN_수불시작일자 = '20200101',
                               @IN_수불종료일자 = '20200131'
 
--------------------------------------------------------*/


CREATE PROCEDURE [sales].[SP_문제2-4_재고수불] 
    @IN_수불시작일자   NVARCHAR(08)
   ,@IN_수불종료일자   NVARCHAR(08)
AS
BEGIN
    ----------------------------------------------------------------------
    -- 실습을 위한 테이블 생성 및 자료 입력 
    -- 임시적 사용을 위해 임시테이블로 생성함 
    ----------------------------------------------------------------------
    CREATE TABLE #제품 (
        제품코드        NVARCHAR(20),
        제품명          NVARCHAR(20)   
    )
    INSERT INTO #제품 (제품코드, 제품명) 
         VALUES ('A','사과'), ('B','포도'), ('C','딸기'),
                ('D','수박'), ('E','참외')
                                

    CREATE TABLE #매입 (
        제품코드        NVARCHAR(20),
        매입일자        NVARCHAR(20),
        매입수량        INT
    )
    INSERT INTO #매입 (제품코드, 매입일자, 매입수량) 
         VALUES ('A', '20191201', 100), ('A', '20200103', 200), 
                ('B', '20200201', 300), ('C', '20200105', 400),
                ('D', '20200107', 500) 

    CREATE TABLE #매출 (
        제품코드        NVARCHAR(20),
        매출일자        NVARCHAR(20),
        매출수량        INT
    )
    INSERT INTO #매출 (제품코드, 매출일자, 매출수량) 
         VALUES ('A', '20191220', 10), ('A', '20200103', 20), 
                ('B', '20200305', 30), ('B', '20200217', 40),
                ('C', '20200220', 50) 

    ----------------------------------------------------------------------
    -- 수불 데이터 집계를 위한 [#수불작업] 테이블 생성 및 INSERT 
    ----------------------------------------------------------------------
    CREATE TABLE #수불작업 (
        제품코드        NVARCHAR(20),
        기초수량        INT DEFAULT 0,
        매입수량        INT DEFAULT 0,
        매출수량        INT DEFAULT 0,
    )
    
   -- 모든 제품이 출력되기 위해 [제품]테이블 정보 INSERT (수량은 모두 0) 
   INSERT INTO #수불작업 (제품코드)
   SELECT A.제품코드 
     FROM #제품 A

   -- 매입데이터 INSERT 
   INSERT INTO #수불작업 (제품코드, 기초수량, 매입수량)
   SELECT A.제품코드, 
          기초수량 = SUM(IIF(A.매입일자 <  @IN_수불시작일자, A.매입수량, 0)),
          매입수량 = SUM(IIF(A.매입일자 >= @IN_수불시작일자 AND A.매입일자 <= @IN_수불종료일자, A.매입수량, 0))
     FROM #매입 A
    GROUP BY A.제품코드      

   -- 매출데이터 INSERT 
   INSERT INTO #수불작업 (제품코드, 기초수량, 매출수량)
   SELECT A.제품코드, 
          기초수량 = SUM(IIF(A.매출일자 <  @IN_수불시작일자, A.매출수량 * (-1), 0)),
          매출수량 = SUM(IIF(A.매출일자 >= @IN_수불시작일자 AND A.매출일자 <= @IN_수불종료일자, A.매출수량, 0))
     FROM #매출 A
    GROUP BY A.제품코드      

    ----------------------------------------------------------------------
    -- 수불결과 집계 및 출력 
    ----------------------------------------------------------------------
    SELECT A.제품코드, B.제품명, 
           기초수량 = SUM(A.기초수량), 
           매입수량 = SUM(A.매입수량), 
           매출수량 = SUM(A.매출수량), 
           기말수량 = SUM(A.기초수량 + A.매입수량 - A.매출수량)
      FROM #수불작업 A 
     INNER JOIN #제품 B ON A.제품코드 = B.제품코드 
     GROUP BY A.제품코드, B.제품명
     ORDER BY A.제품코드

END

GO


